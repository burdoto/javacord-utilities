buildscript {
    repositories.gradlePluginPortal()
    dependencies.classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.0"
}

apply plugin: 'java-library'

group 'de.comroid'
version '1.1.1-SNAPSHOT'

apply from: 'gradle/vars.gradle'

sourceCompatibility = targetCompatibility = 1.8 

tasks.findByName('wrapper').configure {
    gradleVersion = '5.6'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"   
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    
    try {
        archiveClassifier.set 'sources'
    } catch (MissingPropertyException ignored) {
        classifier = "sources"
    }
}

task javadocJar(type: Jar) {
    dependsOn 'javadoc'
    
    from javadoc.destinationDir

    try {
        archiveClassifier.set 'javadoc'
    } catch (MissingPropertyException ignored) {
        classifier = "javadoc"
    }
}

javadoc {
    source = sourceSets.main.java
    
    options {
        encoding = 'UTF-8'
        destinationDirectory(file("./docs/"))
        
        links = [
                "https://docs.oracle.com/javase/8/docs/api/",
                "https://docs.oracle.com/javaee/7/api/"
        ]
    }
}

compileJava.options.encoding = 'UTF-8'
repositories.jcenter()

dependencies {
    compileOnly 'org.jetbrains:annotations:17.0.0'

    testImplementation 'junit:junit:4.12'
}

sourceSets {
    main.java.srcDirs = ["src\\main\\java"]
    test.java.srcDirs = ["src\\test\\java"]
}

apply from: 'gradle/publishing.gradle'
